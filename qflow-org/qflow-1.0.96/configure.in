#!/bin/sh

AC_INIT(VERSION,, eda-dev@opencircuitdesign.com)
AC_PREREQ(2.60)
AC_CONFIG_SRCDIR([Makefile.in])

# Determine the host and build type.
# ===========================================================================
AC_CANONICAL_HOST

PACKAGE=qflow
VERSION=`cat ./VERSION | cut -d. -f1-2`

AC_SUBST(VERSION)
AC_ARG_PROGRAM

# Required programs
# ===========================================================================
AC_PROG_CC
AC_PROG_CPP
AC_ISC_POSIX
if test "x$U" != "x"; then
  AC_MSG_ERROR(Compiler not ANSI compliant)
fi
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_CHECK_PROG(AUTOCONF, autoconf, autoconf, :)
AC_CHECK_PROG(CP, cp, cp, :)
AC_CHECK_PROG(RM, rm, rm, :)

AC_LANG_C
AC_HEADER_STDC
AC_CHECK_FUNCS(setenv putenv)

# Options
# =========================================

dnl Path to Tcl/Tk

AC_ARG_WITH(tclsh,
[  --with-tclsh=DIR		path to tclsh], [
   if test -d "$withval"; then
      TCLSH_DIR=$withval
   elif test -f "$withval"; then
      # Path includes "/tclsh";  remove it
      TCLSH_DIR=${withval%/tclsh}
   else
      TCLSH_DIR=${PATH}
   fi
   AC_CHECK_PROG(TCLSH_PATH, tclsh, tclsh, $TCLSH_DIR)
], [
   AC_CHECK_PROG(TCLSH_PATH, tclsh, tclsh, )])
AC_DEFINE_UNQUOTED(TCLSH_PATH, "$TCLSH_PATH")

dnl Path to Magic 8.0

AC_ARG_WITH(magic,
[  --with-magic=DIR		path to magic-8.0], [
   if test -d "$withval"; then
      QFLOW_MAGIC_DIR=$withval
   elif test -f "$withval"; then
      # Path includes "/magic";  remove it
      QFLOW_MAGIC_DIR=${withval%/magic}
   else
      QFLOW_MAGIC_DIR=${PATH}
   fi
   AC_PATH_PROG(QFLOW_MAGIC_PATH, magic, , $QFLOW_MAGIC_DIR)
], [
   AC_PATH_PROG(QFLOW_MAGIC_PATH, magic, , )])
AC_DEFINE_UNQUOTED(QFLOW_MAGIC_PATH, "$QFLOW_MAGIC_PATH")

dnl Path to Qrouter

AC_ARG_WITH(qrouter,
[  --with-qrouter=DIR		path to qrouter], [
   if test -d "$withval"; then
      QFLOW_QROUTER_DIR=$withval
   elif test -f "$withval"; then
      # Path includes "/qrouter";  remove it
      QFLOW_QROUTER_DIR=${withval%/qrouter}
   else
      QFLOW_QROUTER_DIR=${PATH}
   fi
   AC_PATH_PROG(QFLOW_QROUTER_PATH, qrouter, , $QFLOW_QROUTER_DIR)
], [
   AC_PATH_PROG(QFLOW_QROUTER_PATH, qrouter, , )])
AC_DEFINE_UNQUOTED(QFLOW_QROUTER_PATH, "$QFLOW_QROUTER_PATH")

dnl Path to GrayWolf

AC_ARG_WITH(graywolf,
[  --with-graywolf=DIR		path to graywolf], [
   if test -d "$withval"; then
      QFLOW_GRAYWOLF_DIR=$withval
   elif test -f "$withval"; then
      # Path includes "/graywolf";  remove it
      QFLOW_GRAYWOLF_DIR=${withval%/graywolf}
   else
      QFLOW_GRAYWOLF_DIR=${PATH}
   fi
   AC_PATH_PROG(QFLOW_GRAYWOLF_PATH, graywolf, , $QFLOW_GRAYWOLF_DIR)
], [
   AC_PATH_PROG(QFLOW_GRAYWOLF_PATH, graywolf, , )])
AC_DEFINE_UNQUOTED(QFLOW_GRAYWOLF_PATH, "$QFLOW_GRAYWOLF_PATH")

dnl Path to yosys

AC_ARG_WITH(yosys,
[  --with-yosys=DIR		path to yosys], [
   if test -d "$withval"; then
      QFLOW_YOSYS_DIR=$withval
   elif test -f "$withval"; then
      # Path includes "/yosys";  remove it
      QFLOW_YOSYS_DIR=${withval%/yosys}
   else
      QFLOW_YOSYS_DIR=${PATH}
   fi
   AC_PATH_PROG(QFLOW_YOSYS_PATH, yosys, , $QFLOW_YOSYS_DIR)
], [
   AC_PATH_PROG(QFLOW_YOSYS_PATH, yosys, , )])
AC_DEFINE_UNQUOTED(QFLOW_YOSYS_PATH, "$QFLOW_YOSYS_PATH")

dnl Path to vtr: Simpler alternative to specifying path for abc and Odin_II

AC_ARG_WITH(vtr,
[  --with-vtr=DIR		path to vtr distribution top-level directory], [
   if test -d "$withval"; then
      QFLOW_VTR_DIR=$withval
   else
      QFLOW_VTR_DIR=${PATH}
   fi
   QFLOW_ABC_DIR=${QFLOW_VTR_DIR}/abc_with_bb_support
   QFLOW_ODIN_DIR=${QFLOW_VTR_DIR}/ODIN_II
   AC_PATH_PROG(QFLOW_ODIN_PATH, odin_II.exe, , $QFLOW_ODIN_DIR)
   AC_PATH_PROG(QFLOW_ABC_PATH, abc, , $QFLOW_ABC_DIR)
], [
   AC_PATH_PROG(QFLOW_ABC_PATH, abc, , )
   AC_PATH_PROG(QFLOW_ODIN_PATH, odin_II.exe, , )])

dnl Path to Odin-II

AC_ARG_WITH(odin_II,
[  --with-odin_II=DIR		path to odin_II.exe], [
   if test -d "$withval"; then
      QFLOW_ODIN_DIR=$withval
   elif test -f "$withval"; then
      # Path includes odin_II executable;  strip this off
      QFLOW_ODIN_DIR=${withval%/odin_II.exe}
   else
      QFLOW_ODIN_DIR=${PATH}
   fi
   AC_PATH_PROG(QFLOW_ODIN_PATH, odin_II.exe, , $QFLOW_ODIN_DIR)
], [
   AC_PATH_PROG(QFLOW_ODIN_PATH, odin_II.exe, , )])

AC_DEFINE_UNQUOTED(QFLOW_ODIN_PATH, "$QFLOW_ODIN_PATH")

dnl Path to abc

AC_ARG_WITH(abc,
[  --with-abc=DIR		path to ABC executable], [
   if test -d "$withval"; then
      QFLOW_ABC_DIR=$withval
   elif test -f "$withval"; then
      # Path includes "/abc";  remove it
      QFLOW_ABC_DIR=${withval%/abc}
   else
      QFLOW_ABC_DIR=${PATH}
   fi
   AC_PATH_PROG(QFLOW_ABC_PATH, abc, , $QFLOW_ABC_DIR)
], [
   AC_PATH_PROG(QFLOW_ABC_PATH, abc, , )])
AC_DEFINE_UNQUOTED(QFLOW_ABC_PATH, "$QFLOW_ABC_PATH")

dnl Target library location

AC_ARG_WITH(libdir,
[  --with-libdir=DIR			path to qflow runtime files], [
   QFLOW_LIB_DIR=$withval
], [
   QFLOW_LIB_DIR=${ac_default_prefix}/share/qflow
])

AC_ARG_WITH(bindir,
[  --with-bindir=DIR			path to qflow launch script], [
   QFLOW_BIN_DIR=$withval
], [
   QFLOW_BIN_DIR=${ac_default_prefix}/bin
])

dnl AC_SUBST(QFLOW_QROUTER_PATH)
dnl AC_SUBST(QFLOW_YOSYS_PATH)
dnl AC_SUBST(QFLOW_MAGIC_PATH)
dnl AC_SUBST(QFLOW_ABC_PATH)
dnl AC_SUBST(QFLOW_ODIN_PATH)

AC_SUBST(QFLOW_BIN_DIR)
AC_SUBST(QFLOW_LIB_DIR)

AC_SUBST(TCLSH_PATH)

AC_SUBST(CFLAGS)
AC_SUBST(LDFLAGS)
AC_SUBST(LD)
AC_SUBST(STDLIBS)

if test "x${ac_cv_path_QFLOW_YOSYS_PATH}" == "x"; then
   HAVE_YOSYS=0
else
   HAVE_YOSYS=1
fi

if test "x${ac_cv_path_QFLOW_ODIN_PATH}" == "x"; then
   HAVE_ODIN_II=0
else
   HAVE_ODIN_II=1
fi

if test "x${ac_cv_path_QFLOW_ABC_PATH}" == "x"; then
   HAVE_ABC=0
else
   HAVE_ABC=1
fi

AC_SUBST(HAVE_ABC)
AC_SUBST(HAVE_YOSYS)
AC_SUBST(HAVE_ODIN_II)

AC_OUTPUT(Makefile scripts/Makefile src/Makefile tech/Makefile tech/osu035/Makefile tech/osu050/Makefile)

echo ""
echo "Configuration results:"
echo "----------------------------------------------------"
echo ""

if test "x${ac_cv_path_QFLOW_GRAYWOLF_PATH}" == "x"; then
   echo "ERROR: GrayWolf not found.  Use --with-graywolf=<DIR>"
else
   echo "Using graywolf placement tool at: ${ac_cv_path_QFLOW_GRAYWOLF_PATH}"
fi

if test "x${ac_cv_path_QFLOW_QROUTER_PATH}" == "x"; then
   echo "ERROR: qrouter not found.  Use --with-qrouter=<DIR>"
else
   echo "Using qrouter detail route tool at: ${ac_cv_path_QFLOW_QROUTER_PATH}"
fi

if test "x${ac_cv_path_QFLOW_YOSYS_PATH}" == "x"; then
   if test "x${ac_cv_path_QFLOW_ODIN_PATH}" == "x"; then
      echo "ERROR: yosys not found.  Use --with-yosys=<DIR>"
   fi
else
   echo "Using yosys verilog synthesis tool at: ${ac_cv_path_QFLOW_YOSYS_PATH}"
fi

if test "x${ac_cv_path_QFLOW_ODIN_PATH}" == "x"; then
   if test "x${ac_cv_path_QFLOW_YOSYS_PATH}" == "x"; then
      echo "ERROR: odin_II not found.  Use --with-odin_II=<DIR>"
   fi
else
   echo "Using odin_II verilog parsing tool at: ${ac_cv_path_QFLOW_ODIN_PATH}"
fi

if test "x${ac_cv_path_QFLOW_ABC_PATH}" == "x"; then
   if test "x${ac_cv_path_QFLOW_YOSYS_PATH}" == "x"; then
      echo "ERROR: ABC not found.  Use --with-abc=<DIR>"
   fi
else
   echo "Using ABC logic optimization tool at: ${ac_cv_path_QFLOW_ABC_PATH}"
fi

if test "x${ac_cv_path_QFLOW_MAGIC_PATH}" == "x"; then
   echo "WARNING: Magic 8.0 not found.  Use --with-magic=<DIR>"
else
   echo "Using Magic layout tool at: ${ac_cv_path_QFLOW_MAGIC_PATH}"
fi

echo "----------------------------------------------------"

